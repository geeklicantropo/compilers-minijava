%{
#include <stdio.h>
#include <string.h>
#include "miniJava.h"
%}
%code requires{
#include "Symbols.h"
}

%parse-param{ const IProgram*& progr }

%{
void yyerror ( const IProgram*& p, const char *s );

int yylex();

int yywrap()
{
	return 1;
}

%}

%locations 

%union{
	char Val[256];
	int Int;
	TBinaryOperation BinopType;
	IMainClass* MainClass;
	IProgram* Program;
	IClassDeclareStar* ClassDeclStar;
	IClassDeclare* ClassDecl;
	IVarDeclareStar* VarDeclStar;
	IVarDeclare* VarDecl;
	IMethodDeclareStar* MethodDeclStar;
	IMethodDeclare* MethodDecl;
	IFormalList* FormalList;
	IFormalRestStar* FormalRestStar;
	IStatementStar* StatementStar;
	IStatement* Statement;
	IExpression* Expression;
	IExpList* ExpList;
	const CTypeInfo* TypeInfo;
	const CSymbol* Name;
}

%token <Name> NUMBER
%token CLASS
%token PUBLIC
%token STATIC
%token VOID
%token MAIN
%token STRING 
%token INT_T
%token BOOL_T
%token IF
%token ELSE
%token WHILE
%token SYSOUT
%token LENGTH
%token <Bool> TRUE
%token <Bool> FALSE
%token THIS
%token NEW
%token <Name> ID
%token COMMENT
%token RETURN
%token INTEGERLITERAL
%token EXTENDS


%left <BinopType> BINOP
%left '!'
%left '+' '-'
%left '*' '/'
%left '.' '['

%type <Program> Program
%type <MainClass> MainClass
%type <ClassDeclStar> ClassDeclStar
%type <ClassDecl> ClassDecl
%type <VarDeclStar> VarDeclStar
%type <VarDecl> VarDecl
%type <MethodDeclStar> MethodDeclStar
%type <MethodDecl> MethodDecl
%type <FormalList> FormalList
%type <FormalRestStar> FormalRestStar
%type <StatementStar> StatementStar
%type <Statement> Statement
%type <TypeInfo> Type
%type <Expression> Expression
%type <ExpList> ExpList
%type <ExpList> ExpRest

%%
Program:
	MainClass ClassDeclStar {
		progr = $$ = new CProgram( $1, $2, yylloc.first_line );
	}
	;

MainClass:
	CLASS ID '{' PUBLIC STATIC VOID MAIN '(' STRING '[' ']' ID ')' '{' Statement '}' '}' {
		$$ = new CMainClass( $2,  $12, $15, yylloc.first_line );
	}
	| CLASS ID '{' error '}' {
		printf ( "error1 in Main class in line %d\n", yylloc.first_line ); 
	}
	| CLASS ID '{' PUBLIC STATIC VOID MAIN '(' STRING '[' ']' ID ')' '{' error '}' '}' { 
		printf ( "error2 in Main class in line %d \n", yylloc.first_line ); 
	}
	;

ClassDeclStar:
	{
		$$ = NULL;
	}
	| ClassDeclStar ClassDecl {
		$$ = new CClassDeclareStar( $1, $2, yylloc.first_line );
	}

ClassDecl:
	CLASS ID '{' VarDeclStar MethodDeclStar '}' {
		$$ = new CClassDeclare( $2, $4, $5, yylloc.first_line );
	}
	| CLASS ID EXTENDS ID '{' VarDeclStar MethodDeclStar '}' {
		$$ = new CClassDeclareExtends( $2, $4, $6, $7, yylloc.first_line );
	}
	| CLASS ID '{' error '}'			{ printf ( "error1 in class declaration in line %d\n", yylloc.first_line ); }
	| CLASS ID EXTENDS ID '{' error '}' { printf ( "error2 in class declaration in line %d\n", yylloc.first_line ); }

	;

VarDeclStar: 
	{
		$$ = NULL;
	}
	| VarDeclStar VarDecl {
		$$ = new CVarDeclareStar( $1, $2, yylloc.first_line );
	}
	;

VarDecl:
	Type ID ';' {
		$$ = new CVarDeclare( $1, $2, yylloc.first_line );
	}
	| Type ID error ';' { printf ( "var declaration in line %d\n", yylloc.first_line ); }
	;

MethodDeclStar:
	{
		$$ = NULL;
	}
	| MethodDeclStar MethodDecl {
		$$ = new CMethodDeclareStar( $1, $2, yylloc.first_line );
	}
	;

MethodDecl:
	PUBLIC Type ID '(' FormalList ')' '{' VarDeclStar StatementStar RETURN Expression ';' '}' {
		$$ = new CMethodDeclare( $2, $3, $5, $8, $9, $11, yylloc.first_line );
	}
	;

FormalList:
	{
		$$ = NULL;
	}
	| Type ID FormalRestStar {
		$$ = new CFormalList( $1, $2, $3, yylloc.first_line );
	}
	;

FormalRestStar:
	{
		$$ = NULL;
	}
	| ',' Type ID FormalRestStar {
		$$ = new CFormalRestStar( $2, $3, $4, yylloc.first_line );
	}
	;

Type:
	INT_T '['']' {
		$$ = new CTypeInfo( INTARRAY );
	}
	| BOOL_T {
		$$ = new CTypeInfo( BOOL );
	}
	| INT_T {
		$$ = new CTypeInfo( INT );
	}
	| ID {
		$$ = new CTypeInfo( $1 );
	}
	;

StatementStar:
	{
		$$ = NULL;
	}
	| Statement StatementStar {
		$$ = new CStatementStar( $1, $2, yylloc.first_line );
	}
	;

Statement:
	'{' StatementStar '}' {
		$$ = new CStatement( $2, yylloc.first_line );
	}
	| IF '(' Expression ')' Statement ELSE Statement {
		$$ = new CStatementIf( $3, $5, $7, yylloc.first_line );
	}
	| WHILE '(' Expression ')' Statement {
		$$ = new CStatementWhile( $3, $5, yylloc.first_line );
	}
	| SYSOUT '(' Expression ')' ';' {
		$$ = new CStatementSysOut( $3, yylloc.first_line );
	}
	| ID '=' Expression';' {
		$$ = new CStatementAssignment( $1, $3, yylloc.first_line );
	}
	| ID '[' Expression ']' '=' Expression ';' {
		$$ = new CStatementArrayAssignment( $1, $3, $6, yylloc.first_line );
	}
	| error ';' { printf ("statement error in line %d\n", yylloc.first_line); $$ = new CStatement( 0, yylloc.first_line ); }
	;

Expression:
	Expression BINOP Expression {
		$$ = new CExpressionBinOp( $1, $2, $3, yylloc.first_line );
	}
	| Expression '[' Expression ']' {
		$$ = new CExpressionArray( $1, $3, yylloc.first_line );
	}
	| Expression '.' LENGTH {
		$$ = new CExpressionLength( $1, yylloc.first_line );
	}
	| Expression '.' ID '(' ExpList ')' {
		$$ = new CExpressionCallMethod( $1, $3, $5, yylloc.first_line );
	}
	| NUMBER {
		$$ = new CExpressionNumber( $1, yylloc.first_line );
	}
	| TRUE {
		$$ = new CExpressionBool( true, yylloc.first_line );
	}
	| FALSE {
		$$ = new CExpressionBool( false, yylloc.first_line );
	}
	| ID {
		$$ = new CExpressionVar( $1, yylloc.first_line );
	}
	| THIS {
		$$ = new CExpressionThis( yylloc.first_line );
	}
	| NEW INT_T '[' Expression ']' {
		$$ = new CExpressionNewInt( $4, yylloc.first_line );
	}
	| NEW ID '(' ')' {
		$$ = new CExpressionNewId( $2, yylloc.first_line );
	}
	| '!' Expression {
		$$ =  new CExpressionNegation( $2, yylloc.first_line );
	}
	| '(' Expression ')' {
		$$ = new CExpression( $2, yylloc.first_line );
	}
	;
	 
ExpList:
	{
		$$ = NULL;
	}
	| Expression {
		$$ = new CExpList( $1, yylloc.first_line );
	}
	| ExpRest ',' Expression {
		$$ = new CExpListNext( $1, $3, yylloc.first_line );
	}

ExpRest:
	ExpRest ',' Expression {
		$$ = new CExpListNext( $1, $3, yylloc.first_line );
	}
	| Expression {
		$$ = new CExpList( $1, yylloc.first_line );
	}
	; 
 
%%

void yyerror (const IProgram*& p, const char *s)
{
	//printf("Error in line %d: %s \n", yylloc.first_line, s);
}